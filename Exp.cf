entrypoints Module, Exp ;

comment "--" ;
comment "{-" "-}" ;

layout "where", "let", "split", "mutual" ;
layout stop "in" ;
-- Do not use layout toplevel as it makes pExp fail!

Module.   Module ::= "module" AIdent "where" "{" [Imp] [Decl] "}" ;

Import.   Imp ::= "import" AIdent ;
separator Imp ";" ;

Field.      Fld ::= AIdent "=" Exp ;
separator Fld ",";

DeclDef.    Decl ::= AIdent [AIdent] "=" ExpWhere ;
DeclType.   Decl ::= AIdent ":" Exp ;
DeclData.   Decl ::= "data" AIdent [AIdent] "=" [Label] ;
DeclMutual. Decl ::= "mutual" "{" [Decl] "}" ;
separator   Decl ";" ;

Where.    ExpWhere ::= Exp "where" "{" [Decl] "}" ;
NoWhere.  ExpWhere ::= Exp ;

PrimOp.   Exp  ::= "primitive" String;
Let.      Exp  ::= "let" "{" [Decl] "}" "in" Exp ;
Lam.      Exp  ::= "\\" AIdent [AIdent] "->" Exp ;
Split.    Exp  ::= "split" "{" [Branch] "}" ;
Fun.      Exp1 ::= Exp2 "->" Exp1 ;
Pi.       Exp1 ::= "(" PseudoTDecl ")" "->" Exp1 ;
Record.   Exp1 ::= "[" [PseudoTDecl] "]"  ;
App.      Exp2 ::= Exp2 Exp3 ;
Proj.     Exp3 ::= Exp3 "." AIdent ;
Tuple.     Exp3 ::= "(" [Fld] ")" ;
Var.      Exp3 ::= AIdent ;
U.        Exp3 ::= "U" ;
Real.     Exp3 ::= Double ;
coercions Exp 3 ;

separator Exp "," ;

-- Branches
Branch.   Branch ::= AIdent [AIdent] "->" ExpWhere ;
separator Branch ";" ;

-- Labelled sum alternatives
Label.    Label   ::= AIdent [VTDecl] ;
separator Label "|" ;


-- Telescopes
VTDecl.    VTDecl ::= "(" AIdent [AIdent] ":" Exp ")" ;
terminator VTDecl "" ;

-- Nonempty telescopes with Exp:s, this is hack to avoid ambiguities
-- in the grammar when parsing Pi
PseudoTDecl. PseudoTDecl ::=  Exp ":" Exp ;

separator nonempty PseudoTDecl ";" ;

position token AIdent ((letter|'\''|'_'|'`')(letter|digit|'\''|'_')*) ;
terminator AIdent "" ;

